plugins {
    id 'org.springframework.boot'
}

dependencies {
    implementation project(':common')
    
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.kafka:spring-kafka'
    
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    
    implementation 'org.liquibase:liquibase-core'
    
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.22'
    
    runtimeOnly 'org.postgresql:postgresql:42.7.4'
    
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:kafka'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

def apiSpecDir = "${projectDir}/src/main/resources/api"
def generatedSourcesDir = "${layout.buildDirectory.get()}/generated/sources/openapi"
def generatedPackage = 'by.bsuir.growpathserver.dto'

sourceSets {
    main {
        java {
            srcDirs += "${generatedSourcesDir}/notification-service/src/main/java"
        }
    }
}

configurations {
    openapiGenerator
}

dependencies {
    openapiGenerator 'org.openapitools:openapi-generator-cli:7.5.0'
}

tasks.register("openApiGenerateNotification", JavaExec) {
    doFirst {
        def cliJar = configurations.openapiGenerator.singleFile
        if (!cliJar.exists()) {
            throw new GradleException("OpenAPI Generator CLI JAR not found: ${cliJar.absolutePath}")
        }
        logger.info("Using OpenAPI Generator CLI: ${cliJar.absolutePath}")
    }
    
    def cliJar = configurations.openapiGenerator.singleFile
    def inputFile = file("${apiSpecDir}/notification-api.yaml")
    
    classpath = files(cliJar)
    mainClass = "org.openapitools.codegen.OpenAPIGenerator"
    args = [
        'generate',
        '-i', inputFile.absolutePath,
        '-g', 'spring',
        '-o', "${generatedSourcesDir}/notification-service",
        '--api-package', "${generatedPackage}.api",
        '--model-package', "${generatedPackage}.model",
        '--invoker-package', "${generatedPackage}.invoker",
        '--additional-properties', 'dateLibrary=java8,java8=true,useJakartaEe=true,serializationLibrary=jackson,openApiNullable=false,useBeanValidation=true,hideGenerationTimestamp=true,interfaceOnly=true,useSpringBoot3=true,library=spring-boot,withXml=false',
        '--type-mappings', 'OffsetDateTime=java.time.LocalDateTime,DateTime=java.time.LocalDateTime',
        '--import-mappings', 'OffsetDateTime=java.time.LocalDateTime,DateTime=java.time.LocalDateTime',
        '--global-property', 'models=,apis=,supportingFiles='
    ]
    
    inputs.file(inputFile)
    outputs.dir("${generatedSourcesDir}/notification-service")
}

afterEvaluate {
    tasks.named("compileJava") {
        dependsOn tasks.named("openApiGenerateNotification")
        options.annotationProcessorPath = configurations.annotationProcessor
        options.compilerArgs = [
                '-Amapstruct.suppressGeneratorTimestamp=true',
                '-Amapstruct.suppressGeneratorVersionInfoComment=true'
        ]
    }
}

clean {
    delete layout.buildDirectory.dir("generated")
}
